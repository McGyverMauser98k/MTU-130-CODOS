.ORG = $C975

GETKEY:		STX $02CD		;SAVE REGISTERS X AND Y
			STY $02CE	
			JSR $D092		;DISPLAY CURSOR
			LDX   #$08      ; SET UP 8-BIT COUNT
			LDA   #$01
GET1:    	BIT   $1740
			BNE   GET6 
			BMI   GET1      ; WAIT FOR START BIT
			JSR   DELAY     ; DELAY 1 BIT
GET5:    	JSR   DEHALF    ; DELAY 1/2 BIT TIME
GET2:    	LDA   $1740     ; GET 8 BITS
			AND   #$80      ; MASK OFF LOW ORDER BITS
			LSR   $A0     	; SHIFT RIGHT CHAR
			ORA   $A0
			STA   $A0
			JSR   DELAY     ; DELAY 1 BIT TIME
			DEX  
			BNE   GET2      ; GET NEXT CHAR
			JSR   DEHALF    ; EXIT THIS ROUTINE
			LDX   $02CD
			LDY	  $02CE
			LDA   $A0
			ROL   A         ; SHIFT OFF PARITY
			LSR   A
GET6:    	RTS   

DELAY:	    LDA   $17F3
			STA   $17F4
			LDA   $17F2
DE2:     	SEC   
DE4:     	SBC   #$01 
			BCS   DE3  
			DEC   $17F4
DE3:     	LDY   $17F4
			BPL   DE2
			RTS
			
DEHALF:		LDA   $17F3
			STA   $17F4
			LDA   $17F2
			LSR   A
			LSR   $17F4
			BCC   DE2
			ORA   #$80
			BCS   DE4
			
			
			
C975:                8E CD 02
C978: 8C CE 02 20 92 D0 A2 08
C980: A9 01 2C 40 17 D0 26 30
C988: F9 20 AE C9 20 C5 C9 AD
C990: 40 17 29 80 46 A0 05 A0
C998: 85 A0 20 AE C9 CA D0 EF
C9A0: 20 C5 C9 AE CD 02 AC CE
C9A8: 02 A5 A0 2A 4A 60 AD F3
C9B0: 17 8D F4 17 AD F2 17 38
C9B8: E9 01 B0 03 CE F4 17 AC
C9C0: F4 17 10 F3 60 AD F3 17
C9C8: 8D F4 17 AD F2 17 4A 4E
C9D0: F4 17 90 E3 09 80 B0 E0
